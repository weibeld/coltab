#!/usr/bin/env perl

use strict;
use warnings;
use feature qw(say);
use File::Basename qw(dirname basename);
use Getopt::Std;
use Cwd qw(abs_path);

# Add directory containing our modules (../lib) to @INC so they can be imported
use lib dirname(dirname(abs_path(__FILE__))) . '/lib';
use Weibeld::Coltab::ParseManager qw(parse_file);
use Weibeld::Coltab::HTMLManager qw(init_html set_css get_html);

our $VERSION = "0.01";
my $SCRIPT_NAME = basename($0);

# If set to true, exit script after processing --help or --version flags
$Getopt::Std::STANDARD_HELP_VERSION = 1;

# Command-line options
my %opts;
getopts('t:c:l', \%opts) or abort();
# These vars are true if and only if the corresponding options are specified
my $title = $opts{t};
my $css = $opts{c};
my $is_link_css = $opts{l};

# Callback function references for the ParseManager
my %callbacks = (
    on_header_found => \&on_header_found,
    on_list_item_found => \&on_list_item_found,
    on_list_interrupted => \&on_list_interrupted
);

# Command-line positional arguments
@ARGV == 1 or abort("Error: incorrect number of arguments");
my $markdown = $ARGV[0];

# Do actual work
init_html($title);
set_css($css, $is_link_css) if ($css);
parse_file($markdown, \%callbacks);
#print get_html();


#------------------------------------------------------------------------------#
# Callbacks for ParseManager
#------------------------------------------------------------------------------#
sub on_list_item_found {
    my $list_item = shift;
    _validate_list_item($list_item);
    say "List item found: $list_item";
}

sub on_header_found {
    my($content, $level) = @_;
    say "Header found: $content (level $level)";
}

sub on_list_interrupted {
    say "List interrupted";
}

sub _validate_list_item {
    my $list_item = shift;
    $list_item =~ /^`?\#?([a-f0-9]{3}|[a-f0-9]{6})`?\s*$/i
        or die("Invalid colour: $list_item");
}

#------------------------------------------------------------------------------#
# Subroutines
#------------------------------------------------------------------------------#

# Called by Getopt::Std when supplying --help option
sub HELP_MESSAGE {
    stdo(get_help_message());
}

# Called by Getopt::Std when supplying --version or --help option
sub VERSION_MESSAGE {
    stdo_ln("$SCRIPT_NAME version $VERSION");
}

# Abort script due to error (e.g. invalid command-line options)
sub abort {
    my $msg = shift;
    stde_ln($msg) if ($msg);
    stde(get_help_message());
    exit 1;
}

# Get help message
sub get_help_message {
    return <<"EOF"
Usage:
  $SCRIPT_NAME [options] <markdown-file>

Options:
  -t <title>  Title of HTML output file
  -c <file>   CSS file
  -l          Only link to CSS file (rather than including it)
  --help      Show this screen
  --version   Show version information
EOF
}

# Print argument to stdout or stderr, respectively
sub stdo { print STDOUT shift; }
sub stde { print STDERR shift; }
sub stdo_ln { say STDOUT shift; }
sub stde_ln { say STDERR shift; }

